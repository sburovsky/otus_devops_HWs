
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОстаткиТоваров.Очистить();
	Движения.ПартииТоваров.Очистить();
	Движения.Продажи.Очистить();
	
	ПроведениеДокументов.ПроверкаОперативногоСписания(ЭтотОбъект, Режим, Отказ);	
	// Учет партий товаров
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.СформироватьПартионноеСписание(ЭтотОбъект, Режим);
	
	Движения.ПартииТоваров.Записать();  //временная таблица Движения.ПартииТоваров при использовании запишется на диск, поэтому можно работать с реальной таблицей
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТоваровСписокТоваров.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТоваровСписокТоваров.Количество) КАК Количество,
		|	СУММА(ПродажаТоваровСписокТоваров.Выручка) КАК Выручка
		|ПОМЕСТИТЬ ДокТЧ
		|ИЗ
		|	Документ.ПродажаТоваров.СписокТоваров КАК ПродажаТоваровСписокТоваров
		|ГДЕ
		|	ПродажаТоваровСписокТоваров.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТоваровСписокТоваров.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокТЧ.Номенклатура КАК Номенклатура,
		|	ДокТЧ.Количество КАК Количество,
		|	ДокТЧ.Выручка КАК Выручка,
		|	ПартииТоваровОбороты.СтоимостьРасход КАК Себестоимость
		|ИЗ
		|	ДокТЧ КАК ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваров.Обороты(&МоментВремени, &МоментВремени, Регистратор, ) КАК ПартииТоваровОбороты
		|		ПО ДокТЧ.Номенклатура = ПартииТоваровОбороты.Номенклатура
		|ГДЕ
		|	ПартииТоваровОбороты.Регистратор = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Движения.Продажи.Записывать = истина;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
		Движение.Выручка = ВыборкаДетальныеЗаписи.Выручка;
		Движение.Себестоимость = ВыборкаДетальныеЗаписи.Себестоимость;
	КонецЦикла;
		
КонецПроцедуры
